ext {
    kotlin_version = '1.3.72'
    kotlinx_version = '1.1.0'
    coroutines_version = '1.3.7'
    serialization_version = '0.20.0'
    ktor_version = '1.3.2'
}

repositories {
    google()
    jcenter()
    mavenCentral()
    maven { url 'https://kotlin.bintray.com/ktor' }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.0'

    defaultConfig {
        applicationId 'org.jetbrains.kotlin.mpp_app_android'
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName '1.0'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    packagingOptions {
        exclude("META-INF/*.kotlin_module")
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'
    testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.4.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
}

kotlin {
    android("android")
    iosArm64("ios") { binaries { framework() } }
    iosX64("macOS") { binaries { framework() } }

    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$serialization_version"
                api "io.ktor:ktor-client-core:$ktor_version"
                api "io.ktor:ktor-client-json:$ktor_version"
                api "io.ktor:ktor-client-serialization:$ktor_version"
            }
        }

        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
                implementation "io.ktor:ktor-client-mock:$ktor_version"
            }
        }

        androidMain {
            dependsOn commonMain
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"
                api "io.ktor:ktor-client-core-jvm:$ktor_version"
                api "io.ktor:ktor-client-json-jvm:$ktor_version"
                api "io.ktor:ktor-client-serialization-jvm:$ktor_version"
            }
        }

        androidTest {
            dependsOn commonTest
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
                implementation "org.jetbrains.kotlin:kotlin-test"
                implementation "org.jetbrains.kotlin:kotlin-test-junit"
                implementation "io.ktor:ktor-client-mock-jvm:$ktor_version"
            }
        }

        iosMain {
            dependsOn commonMain
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:$serialization_version"
                api "io.ktor:ktor-client-ios:$ktor_version"
            }
        }

        iosTest {
            dependsOn commonTest
            dependencies {
                implementation "io.ktor:ktor-client-mock-native:$ktor_version"
            }
        }

        iosArm64().compilations.main.defaultSourceSet {
            dependsOn iosMain
            dependencies {
                api "io.ktor:ktor-client-ios-iosarm64:$ktor_version"
                api "io.ktor:ktor-client-json-iosarm64:$ktor_version"
                api "io.ktor:ktor-client-serialization-iosarm64:$ktor_version"
            }
        }

        iosArm64().compilations.test.defaultSourceSet {
            dependsOn iosTest
        }

        iosX64().compilations.main.defaultSourceSet {
            dependsOn iosMain
            dependencies {
                api "io.ktor:ktor-client-ios-iosx64:$ktor_version"
                api "io.ktor:ktor-client-json-iosx64:$ktor_version"
                api "io.ktor:ktor-client-serialization-iosx64:$ktor_version"
            }
        }

        iosX64().compilations.test.defaultSourceSet {
            dependsOn iosTest
        }

        all {
            languageSettings {
                useExperimentalAnnotation('kotlin.Experimental')
            }
        }
    }
}

// task iosTest {
//     def device = project.findProperty("device")?.toString() ?: "iPhone 8"
//     dependsOn 'linkDebugTestIosX64'
//     group = JavaBasePlugin.VERIFICATION_GROUP
//     description = "Execute unit tests on ${device} simulator"
//     doLast {
//         def binary = kotlin.targets.iosX64.binaries.getTest('DEBUG')
//         exec { commandLine 'xcrun', 'simctl', 'spawn', device, binary.outputFile }
//     }
// }

// This task attaches native framework built from ios module to Xcode project
// (see iosApp directory). Don't run this task directly,
// Xcode runs this task itself during its build process.
// Before opening the project from iosApp directory in Xcode,
// make sure all Gradle infrastructure exists (gradle.wrapper, gradlew).
task copyFramework {
    def buildType = project.findProperty('kotlin.build.type') ?: 'DEBUG'
    def target = project.findProperty('kotlin.target') ?: 'ios'
    dependsOn kotlin.targets."$target".binaries.getFramework(buildType).linkTask

    doLast {
        def srcFile = kotlin.targets."$target".binaries.getFramework(buildType).outputFile
        def targetDir = getProperty('configuration.build.dir')
        copy {
            from srcFile.parent
            into targetDir
            include 'kotlinMP.framework/**'
            include 'kotlinMP.framework.dSYM'
        }
    }
}
